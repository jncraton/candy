import stdlib
import stdio
import string
import fcntl

#ifdef _WIN32
#include <io.h>
#endif

unsigned char buf[256]
unsigned char buf_start = 255
unsigned char buf_len = 0;
unsigned char is_preprocessor_line = false
unsigned char is_single_quoted = false
unsigned char is_double_quoted = false
unsigned char literal = false

int in_regular_code():
    return not is_double_quoted and not is_single_quoted and not is_preprocessor_line

void fill_buffer():
    if not (buf_len = fread(buf, 1, 254, stdin)):
        exit(0)

unsigned char get_byte(unsigned char i):
    return buf[(unsigned char)(buf_start + i)]

unsigned char read_next_byte():
    unsigned char byte[2]

    buf_start++

    if not buf_len:
        return false
    
    buf_len--
    
    if fread(byte, 1, 1, stdin):
        buf[(unsigned char)(buf_start - 2)] = byte[0]
        buf_len++
    
    return true

int next_line_indent():
    for (unsigned char i = 0; i < 255; i++):
        if (get_byte(i) == '\n' and get_byte(i+1) != '\n'):
            unsigned char count
            for (count = 0; get_byte(i + count + 1) == ' '; count++)
            return count >> 2

    return false

int is_valid_name_char(unsigned char c):
    return (c >= 48 and c <= 57) or (c >= 65 and c <= 90) or (c >= 97 and c <= 122) or c == '_'

int replace_keyword(const char* keyword, const char* replacement):
    if not in_regular_code():
        return false
    
    if is_valid_name_char(get_byte(-1)):
        return false
    
    if is_valid_name_char(get_byte(strlen(keyword))):
        return false
    
    for (int i = 0; i < strlen(keyword); i++):
        if get_byte(i) != keyword[i]:
            return false
    
    printf(replacement)
    
    for (int i = 0; i < strlen(keyword) - 1; i++):
        read_next_byte()
    
    buf[buf_start] = replacement[strlen(replacement) - 1]
    
    return true

int main (int argc, char **argv):
    int line_pos = false
    unsigned char open_braces = false
    unsigned char previous_indent = false
    unsigned char needs_closing_paren = false
    unsigned char needs_closing_imp = false

    fill_buffer()

    #ifdef _WIN32
    // Work around newline replacement on Windows
    extern int fileno(FILE*)
    setmode(fileno(stdout), O_BINARY)
    #endif

    while (read_next_byte()):
        if not is_preprocessor_line:
            // Handle semicolon insertions
            if (get_byte(0) == '\n' and
                get_byte(-1) != ';' and
                get_byte(-1) != ',' and
                get_byte(-1) != '&' and
                //get_byte(-1) != '+' and
                //get_byte(-1) != '-' and
                get_byte(-1) != '*' and
                get_byte(-1) != '/' and
                get_byte(-1) != '>' and
                get_byte(-1) != '<' and
                get_byte(-1) != '|' and
                get_byte(-1) != '{' and
                get_byte(-1) != '}' and
                get_byte(-1) != ' ' and
                get_byte(-1) != '\n' and
                get_byte(-1) != ':'):
                printf(";")

        if get_byte(0) == ':' and get_byte(1) == '\n':
            if needs_closing_paren:
                printf(")")
                needs_closing_paren = false
            printf(" {")
            open_braces++
        else if get_byte(0) == '\n':
            if needs_closing_imp:
                printf(".h\"")
                needs_closing_imp = false
            printf("\n")
        else if replace_keyword("if ", "if ("):
            needs_closing_paren = true
        else if replace_keyword("import", "#include"):
            printf(" \"")
            read_next_byte()
            needs_closing_imp = true
            is_preprocessor_line = true
        else if get_byte(0) == '"' and !literal and !is_single_quoted and !is_double_quoted:
            printf("(\"")
        else if replace_keyword("    pass", ""):
            pass
        else if replace_keyword("not", "!"):
            pass
        else if replace_keyword("and", "&&"):
            pass
        else if replace_keyword("true", "(1)"):
            pass
        else if replace_keyword("false", "(0)"):
            pass
        else if replace_keyword("or", "||"):
            pass
        else:
            printf("%c", get_byte(0))
        
        if not is_preprocessor_line:
            // Handle closing brace insertion
            if get_byte(0) == '\n':
                while (open_braces and previous_indent > next_line_indent()):
                    for (unsigned char i = 4; i < previous_indent << 2; i++):
                        printf(" ")

                    printf("}\n")
                    previous_indent--
                    open_braces--

                previous_indent = next_line_indent()
                #ifdef debug
                printf("// Next indent level: %d\n", previous_indent)
                #endif

            if (get_byte(0) == '"' and !literal and !is_single_quoted):
                if is_double_quoted:
                    printf(")")
            
        if get_byte(0) == '\n':
            is_preprocessor_line = false

        if (get_byte(0) == '#' and !(is_single_quoted || is_double_quoted)):
            is_preprocessor_line = true

        if (get_byte(0) == '\'' and !literal and !is_double_quoted):
            is_single_quoted = !is_single_quoted

        if (get_byte(0) == '"' and !literal and !is_single_quoted):
            is_double_quoted = !is_double_quoted
        
        if get_byte(0) == '\\' and !literal:
            literal = true
        else:
            literal = false
        
        line_pos +=1

    return false
